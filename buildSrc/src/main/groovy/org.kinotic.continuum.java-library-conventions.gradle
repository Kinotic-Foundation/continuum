plugins {
    // Apply the common convention plugin for shared build configuration between library and application projects.
    id 'org.kinotic.continuum.java-common-conventions'

    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    // We publish the common dependencies in both this and the application conventions plugin. Otherwise the platform did not end up in consumers of these projects..
    /** Begin Common Dependencies **/
    api platform(project(':continuum-platform'))
    //noinspection DependencyNotationArgument
    annotationProcessor platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.apache.commons:commons-text'

    implementation "ch.qos.logback:logback-core"
    implementation "ch.qos.logback:logback-classic"

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    /** End Common Dependencies **/
}


java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java

            pom {
                name = 'Continuum Framework'
                description = 'A Microservice and IOT framework for everyone!'
                url = 'https://continuumframework.org'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Kinotic Developers'
                        email = 'develop@kinotic.org'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Kinotic-Foundation/continuum-framework.git'
                    developerConnection = 'scm:git:https://github.com/Kinotic-Foundation/continuum-framework.git'
                    url = 'https://github.com/Kinotic-Foundation/continuum-framework'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('MAVEN_USERNAME')
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}


javadoc {
    // suppress warnings
    options.addStringOption('Xdoclint:none', '-quiet')

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
